# Simulator dejagnu utilities.

# Print the version of the simulator being tested.
# Required by dejagnu.

proc sim_version {} {
    global SIM
    set version 0.5
    set program [which $SIM]
    if { "$program" == "0" } {
	clone_output "$SIM $version\n"
    } else {
	clone_output "$program $version\n"
    }
}

# Run the simulator
# Required by dejagnu.
# FIXME: wip, currently unused.

proc sim_run { prog sim_opts redir } {
    global AS ASFLAGS LD LDFLAGS SIM SIMFLAGS
    global comp_output
    global srcdir
    global subdir

    verbose "Executing $srcdir/lib/run $AS $ASFLAGS $as_opts $srcdir/$subdir/$prog $redir"
    catch "exec $srcdir/lib/run $AS $ASFLAGS $as_opts $srcdir/$subdir/$prog $redir" comp_output
    verbose "output was $comp_output"
    return [list $comp_output ""];
}

# Initialize the testrun.
# Required by dejagnu.

proc sim_init { args } {
    # Need to return an empty string (copied from GAS).
    return ""
}

# Run testcase NAME.
# NAME is either a fully specified file name, or just the file name in which
# case $srcdir/$subdir will be prepended.
# The file can contain options in the form "# option(mach list): value"
# Possibilities:
# mach(): machine names
# as(mach): <assembler options>
# ld(mach): <linker options>
# sim(mach): <simulator options>
# output(): program output pattern to match with string-match
# If `output' is not specified, the program must output "pass".

proc run_sim_test { name } {
    global subdir srcdir
    global AS ASFLAGS LD LDFLAGS SIM SIMFLAGS
    global opts

    if [string match "*/*" $name] {
	set file $name
	set name [file tail $name]
    } else {
	set file "$srcdir/$subdir/$name"
    }

    set opt_array [slurp_options "${file}"]
    if { $opt_array == -1 } {
	unresolved $subdir/$name
	return
    }
    set opts(as) {}
    set opts(ld) {}
    set opts(sim) {}
    set opts(output) {}
    set opts(mach) {}

    foreach i $opt_array {
	set opt_name [lindex $i 0]
	set opt_machs [lindex $i 1]
	set opt_val [lindex $i 2]
	if ![info exists opts($opt_name)] {
	    perror "unknown option $opt_name in file $file"
	    unresolved $subdir/$name
	    return
	}
	foreach m $opt_machs {
	    set opts($opt_name,$m) $opt_val
	}
	if { "$opt_machs" == "" } {
	    set opts($opt_name) $opt_val
	}
    }

    set testname $name
    set sourcefile $file
    if { $opts(output) == "" } {
	set opts(output) pass
    }

    foreach mach $opts(mach) {
	verbose "Testing $name on $mach."

	if ![info exists opts(as,$mach)] {
	    set opts(as,$mach) $opts(as)
	}
	send_log "$AS $ASFLAGS $opts(as,$mach) -I$srcdir/$subdir -o ${name}.o $sourcefile\n"
	catch "exec $AS $ASFLAGS $opts(as,$mach) -I$srcdir/$subdir -o ${name}.o $sourcefile" comp_output

	if ![string match "" $comp_output] {
	    verbose -log "$comp_output" 3
	    fail "$mach $testname"
	    return
	}

	if ![info exists opts(ld,$mach)] {
	    set opts(ld,$mach) $opts(ld)
	}
	send_log "$LD $LDFLAGS $opts(ld,$mach) -o ${name}.x ${name}.o\n"
	catch "exec $LD $LDFLAGS $opts(ld,$mach) -o ${name}.x ${name}.o" comp_output

	if ![string match "" $comp_output] {
	    verbose -log "$comp_output" 3
	    fail "$mach $testname"
	    return
	}

	if ![info exists opts(sim,$mach)] {
	    set opts(sim,$mach) $opts(sim)
	}
	send_log "$SIM $SIMFLAGS $opts(sim,$mach) ${name}.x\n"
	catch "exec $SIM $SIMFLAGS $opts(sim,$mach) ${name}.x" comp_output

	if ![string match $opts(output) $comp_output] {
	    verbose -log "output:  $comp_output" 3
	    verbose -log "pattern: $opts(output)" 3
	    fail "$mach $testname"
	    return
	}

	pass "$mach $testname"
    }
}

proc slurp_options { file } {
    if [catch { set f [open $file r] } x] {
	#perror "couldn't open `$file': $x"
	perror "$x"
	return -1
    }
    set opt_array {}
    # whitespace expression
    set ws  {[ 	]*}
    set nws {[^ 	]*}
    # whitespace is ignored anywhere except within the options list;
    # option names are alphabetic only
    set pat "^#${ws}(\[a-zA-Z\]*)\\((.*)\\)$ws:${ws}(.*)$ws\$"
    # Allow comment as first line of file.
    set firstline 1
    while { [gets $f line] != -1 } {
	set line [string trim $line]
	# Whitespace here is space-tab.
	if [regexp $pat $line xxx opt_name opt_machs opt_val] {
	    # match!
	    lappend opt_array [list $opt_name $opt_machs $opt_val]
	} else {
	    if { ! $firstline } {
		break
	    }
	}
	set firstline 0
    }
    close $f
    return $opt_array
}
